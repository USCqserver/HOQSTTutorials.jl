{
  "cells": [
    {
      "cell_type": "markdown",
      "source": [
        "## Model setup\nIn this example notebook, we solve the master equation of the following form\n\n$$\\dot{\\rho} = - i [-Z + \\delta(s)Z, \\rho] + \\mathcal{L}(\\rho)$$\n\nvia the quantum trajectories method. In above expression, $\\mathcal{L}$ is the Davies generator from the adiabatic master equation. $\\delta(s)$ is a classical stochastic process generated by summing multiple telegraph processes together:\n\n$$\\delta(t) = \\sum_{i=1}^N T_i(t) \\ ,$$\n\nwhere $T_i(t)$ switches randomly between $\\pm b_i$​ with a rate $\\gamma_i$​. In the following code block, we show how to construct such a process:"
      ],
      "metadata": {}
    },
    {
      "outputs": [],
      "cell_type": "code",
      "source": [
        "using OrdinaryDiffEq, OpenQuantumTools\nusing Plots, StatsBase\n\nH = DenseHamiltonian([(s)->1.0], [-σz], unit=:ħ)\nu0 = PauliVec[1][1]\n\ncoupling = ConstantCouplings([\"Z\"], unit=:ħ)\n# number of fluctuators\nnum = 10\n# The values of b created here are in angular frequencies unit\nbvec = 0.01 * ones(num)\n# γᵢ\nγvec = log_uniform(0.01, 1, num)\n# create the fluctuator coupling interaction\nfluctuator_ensemble = EnsembleFluctuator(bvec, γvec);\ninteraction_fluctuator = Interaction(coupling, fluctuator_ensemble)\n# create the Ohmic coupling interaction\nohmic_bath = Ohmic(1e-4, 4, 16)\ninteraction_ohmic = Interaction(coupling, ohmic_bath)\n# merge those two bath object into `InteractionSet`\ninteractions = InteractionSet(interaction_fluctuator, interaction_ohmic)\nannealing = Annealing(H, u0, interactions=interactions)"
      ],
      "metadata": {},
      "execution_count": null
    },
    {
      "cell_type": "markdown",
      "source": [
        "## Dynamics\nWe solve the dynamics and calculate $\\langle X \\rangle$ during the evolution:"
      ],
      "metadata": {}
    },
    {
      "outputs": [],
      "cell_type": "code",
      "source": [
        "tf = 100\nprob = build_ensembles(annealing, tf, :ame, ω_hint = range(-2, 2, length=100))\nt_list = range(0,tf,length=200)\nsol = solve(prob, Tsit5(), EnsembleSerial(), trajectories=1000, reltol=1e-6, saveat=t_list)\n\ndataset = []\nst(s, so) = normalize(so(s, continuity=:left))\nfor s in t_list\n    push!(dataset, [real(st(s, so)' * σx * st(s, so)) for so in sol])\nend\n\npop_mean = []\npop_rmse = []\nfor data in dataset\n    p_mean = sum(data)/1000\n    p_rmse = sqrt(sum((x)->(x-p_mean)^2, data))/1000\n    push!(pop_mean, p_mean)\n    push!(pop_rmse, 2*p_rmse)\nend"
      ],
      "metadata": {},
      "execution_count": null
    },
    {
      "cell_type": "markdown",
      "source": [
        "We also solve the dynamics with pure Ohmic bath, i.e. the adiabatic master equation:"
      ],
      "metadata": {}
    },
    {
      "outputs": [],
      "cell_type": "code",
      "source": [
        "a_list = range(0,tf,length=400)\nannealing_ame = Annealing(H, u0, coupling=coupling, bath=ohmic_bath)\nsol_ame = solve_ame(annealing_ame, tf, alg=Tsit5(), ω_hint = range(-2, 2, length=100), \n    reltol=1e-6, saveat=a_list)\name_x = [real(tr(u*σx)) for u in sol_ame.u]"
      ],
      "metadata": {},
      "execution_count": null
    },
    {
      "cell_type": "markdown",
      "source": [
        "We compare $\\langle X \\rangle$ obtained using the above two models:"
      ],
      "metadata": {}
    },
    {
      "outputs": [],
      "cell_type": "code",
      "source": [
        "plot(a_list, ame_x, label=\"Ohmic\", linewidth=2)\nplot!(t_list, pop_mean, ribbon=pop_rmse, label=\"Hyrbrid\", linewidth=2)\nxlabel!(\"s (ns)\")\nylabel!(\"<X>\")"
      ],
      "metadata": {},
      "execution_count": null
    }
  ],
  "nbformat_minor": 2,
  "metadata": {
    "language_info": {
      "file_extension": ".jl",
      "mimetype": "application/julia",
      "name": "julia",
      "version": "1.5.3"
    },
    "kernelspec": {
      "name": "julia-1.5",
      "display_name": "Julia 1.5.3",
      "language": "julia"
    }
  },
  "nbformat": 4
}
